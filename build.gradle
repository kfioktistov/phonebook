buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.4.3.RELEASE'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'docker'

group = 'db.sample'

jar {
    baseName = 'phonebook'
    version =  '0.1.0'
}

repositories {
    mavenCentral()
}

test {
    dependsOn 'cleanTest'
    useJUnit {
        includeCategories 'com.lessons.db.phonebook.tests.categories.Unit'
    }
}

task integrationTest(type: Test) {
    useJUnit {
        includeCategories 'com.lessons.db.phonebook.tests.categories.Integration'
    }
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task buildDockerImage(type: Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}